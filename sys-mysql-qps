#!/usr/bin/env perl

=head1 NAME

sys-mysql-qps - Get MySQL status, qps and conn ...

=head1 SYNOPSIS

Usage: [OPTION...]

=head1 RISKS

 Need to get MySQL global status. 

=cut

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use English '-no_match_vars';
use POSIX qw(strftime);
use Data::Dumper;

my $host     = '127.0.0.1';
my $port     = 3306;
my $user     = 'root';
my $password = '';
my $askpass  = 0;
my $database = 'information_schema';
my $charset  = 'utf8';
my $help     = 0;
my $verbose  = 0;
my $interval = 5;

GetOptions(
    "host|h=s"     => \$host,
    "port|P=i"     => \$port,
    "user|u=s"     => \$user,
    "password|p=s" => \$password,
    "askpass!"     => \$askpass,
    "database|d=s" => \$database,
    "charset|c=s"  => \$charset,
    "verbose|v!"   => \$verbose,
    "help!"        => \$help,
    "interval|i"   => \$interval,
) or die "option error: $!";

if ($help) {
    usage($0);
}

sub usage {
    my $name = shift;
    system("perldoc $name");
    exit 0;
}

if ($askpass) {
   $password = noecho_password("Enter password ");
}

if ( !$password ) {
    warn "Can not connect to MySQL without password.";
    usage($0);
    exit 1;
}

sub noecho_password {
    my $prompt = shift;
    local $OUTPUT_AUTOFLUSH = 1;
    my $response;
    eval {
        require Term::ReadKey;
        Term::ReadKey->import(qw( ReadMode ReadKey ));
        if ($OSNAME =~ m/MSWin/) {
            require Win32::Console::ANSI;
        }
        print "$prompt: ";
        ReadMode('noecho');
        $response = <STDIN>;
        chomp($response);
        ReadMode('normal');
        print "\n" or die 
            "Cannot print: $OS_ERROR";
    };
    if ($EVAL_ERROR) {
        die "Cannot read respose; is Term::ReadKey installed? $EVAL_ERROR";
    }
    return $response;
}

sub get_time {
    return strftime("%H:%M:%S", localtime(time));
}

sub round_value {
    my $value = shift @_;
    return int($value);
}

sub print_state {
    my ($time, $state_info) = @_;
    printf("%s:%d %-10s| %5d%5d%5d%6d%7d | %8d%8d%8d%8d | %4d%5d%5d | %10d bytes%10d bytes\n",
    $host, $port, $time, $state_info->{ins}, $state_info->{upd}, $state_info->{del}, 
    $state_info->{sel}, $state_info->{qps}, $state_info->{rins}, $state_info->{rupd}, 
    $state_info->{rdel}, $state_info->{rread}, $state_info->{run}, $state_info->{conn}, 
    $state_info->{cre}, $state_info->{recv}, $state_info->{send});
}

my %db_info = (
    'Questions'      => 0,
    'Com_select'     => 0,
    'Com_insert'     => 0,
    'Com_update'     => 0,
    'Com_delete'     => 0,
    'Bytes_received' => 0,
    'Bytes_sent'     => 0,
    'Questions'      => 0,
    'Threads_connected'    => 0,
    'Threads_running'      => 0,
    'Innodb_rows_inserted' => 0,
    'Innodb_rows_read'     => 0,
    'Innodb_rows_updated'  => 0,
    'Innodb_rows_deleted'  => 0,
    'Threads_created'      => 0,
);

my %db_state = (
    'qps'   => 0,
    'sel'   => 0,
    'ins'   => 0,
    'upd'   => 0,
    'del'   => 0,
    'rins'  => 0,
    'rread' => 0,
    'rupd'  => 0,
    'rdel'  => 0,
    'conn'  => 0,
    'cre'   => 0,
    'run'   => 0,
    'recv'  => 0,
    'send'  => 0,
);

# get db handle
printf( "connect to %s, %d, %s, xxxxxxxx ...\n", $host, $port, $user )
  if $verbose;
my $dbpre = MySQL::dbh->new(
    host     => $host,
    port     => $port,
    user     => $user,
    password => $password,
    charset  => $charset,
    driver   => 'mysql',
);

my $dbh = $dbpre->get_dbh( $database, { AutoCommit => 1 } ) or
                            die "Can't get dbh.. $host:$port";

my $sql_status_info =<<"SQL_END";
SHOW GLOBAL STATUS 
WHERE variable_name IN 
   ('Questions', 'Com_select', 'Com_update', 'Com_insert'
    'Com_delete', 'Threads_connected', 'Threads_created', 'Threads_running', 
    'Innodb_rows_inserted', 'Innodb_rows_read', 'Innodb_rows_updated', 
    'Innodb_rows_deleted', 'Bytes_received','Bytes_sent')
SQL_END

my $addr_len = length($host) + length($port) + 12;
print   " " x $addr_len . "|"
       . " " x 10 . '-- QPS -- ' . " " x 10 . "|"
       . " " x 5  . '-- Innodb Rows Status --' . " " x 5 . "|"
       . " " x 1  . '-- Threads --' . " " x 2 . "|"
       . " " x 9 . " -- Bytes --" . " " x 9
       . "\n";

printf("%s%-12s|%6s%5s%5s%6s%7s | %8s%8s%8s%8s | %4s%5s%5s | %-15s%-15s\n",
       " " x ($addr_len - 20) . 'addr' . " " x 4, 'time','ins', 'upd', 'del', 'sel', 'qps', 
       'ins', 'upd', 'del', 'read', 'run', 'conn', 'cre', " " x 5 . 'recv', " " x 5 . 'send');


my $n = 10;
while ($n--) {
   my $item_info = $dbh->selectall_arrayref($sql_status_info);
   foreach my $status (@$item_info) {
       my ($name, $value) = @$status; 
       eval {
           my $diff = $value - $db_info{$name};
           my $last_value = round_value( ($diff >= 0 ? $diff : $value)/$interval );
           if ( $name eq 'Bytes_received') {
               $db_state{'recv'} = $last_value;
           }
           elsif ( $name eq 'Bytes_sent' ) {
               $db_state{'send'} = $last_value;
           }
           elsif ( $name eq 'Com_delete' ) {
               $db_state{'del'}  = $last_value;
           }
           elsif ( $name eq 'Com_insert' ) {
               $db_state{'ins'}  = $last_value;
           }
           elsif ( $name eq 'Com_update' ) {
               $db_state{'upd'}  = $last_value;
           }
           elsif ( $name eq 'Com_select' ) {
               $db_state{'sel'}  = $last_value;
           }
           elsif ( $name eq 'Questions' ) {
               $db_state{'qps'}  = $last_value;
           }
           elsif ( $name eq 'Innodb_rows_deleted' ) {
               $db_state{'rdel'} = $last_value;
           }
           elsif ( $name eq 'Innodb_rows_insert' ) {
               $db_state{'rins'} = $last_value;
           }
           elsif ( $name eq 'Innodb_rows_update' ) {
               $db_state{'rupd'} = $last_value;
           }
           elsif ( $name eq 'Innodb_rows_select' ) {
               $db_state{'rread'} = $last_value;
           }
           elsif ( $name eq 'Threads_connected' ) {
               $db_state{'conn'} = $last_value;
           }
           elsif ( $name eq 'Threads_created' ) {
               $db_state{'cre'} = $last_value;
           }
           elsif ( $name eq 'Threads_running' ) {
               $db_state{'run'} = $last_value;
           }
           else {

           }

           $db_info{$name} = $value;
       };

       if ($@) {
           print "error: $@";
       }
   }
   print_state( get_time(), \%db_state ) if $n < 9;
   sleep $interval;
}
# ###################################################################
# Documentation.
# ###################################################################

=head1 OPTIONS

=over 4

=item host=s | h

MySQL host address, default value is '127.0.0.1'

=item port=i | P

MySQL port number. default value is 3306

=item user=s | u

MySQL username. default value is monitor

=item password=s | p

MySQL user password. no default value.

=item askpass

prompt for password options when connect to MySQL.

=item database=s | D

MySQL database will be used, default is 'information_schema'.

=item charset=s | A

The character used when connect to MySQL, default value is UTF8.

=item verbose|v

print verbose info.

=item help

print help info.

=item interval | i

specify the interval time that collect db status, default is 5 seconds.

=back

=head1 AUTHOR

zhe.chen <chenzhe07@gmail.com>

=head1 CHANGELOG

v0.1.0 version

=cut

package MySQL::dbh;
# Get the database handle which user use, and this database
# handle object should be destroy when leave MySQL database.

use strict;
use warnings FATAL => 'all';
use constant PTDEBUG => $ENV{PTDEBUG} || 0;
use English qw(-no_match_vars);
use DBI;
use vars qw($VERSION @ISA @EXPORT @EXPORT_OK);

use Data::Dumper;
$Data::Dumper::Indent    = 1;
$Data::Dumper::Sortkeys  = 1;
$Data::Dumper::Quotekeys = 0;

require Exporter;
@ISA     = qw(Exporter);
@EXPORT  = qw( get_dbh disconnect );
$VERSION = '0.1.0';

eval { require DBI; };

if ($@) {
    die "Cannot connect to MySQL because the Perl DBI module is not "
      . "installed or not found.  Run 'perl -MDBI' to see the directories "
      . "that Perl searches for DBI.  If DBI is not installed, try:\n"
      . "  Debian/Ubuntu  apt-get install libdbi-perl\n"
      . "  RHEL/CentOS    yum install perl-DBI\n"
      . "  OpenSolaris    pkg install pkg:/SUNWpmdbi\n";
}

sub host {
    my $self = shift;
    $self->{host} = shift if @_;
    return $self->{host};
}

sub port {
    my $self = shift;
    $self->{port} = shift if @_;
    return $self->{port};
}

sub user {
    my $self = shift;
    $self->{user} = shift if @_;
    return $self->{user};
}

sub password {
    my $self = shift;
    $self->{password} = shift if @_;
    return $self->{password};
}

sub charset {
    my $self = shift;
    $self->{charset} = shift if @_;
    return $self->{charset};
}

sub driver {
    my $self = shift;
    $self->{driver} = shift if @_;
    return $self->{driver};
}

sub new {
    my ( $class, %args ) = @_;
    my @required_args = qw(host port user password);
    PTDEBUG && print Dumper(%args);

    foreach my $arg (@required_args) {
        warn "I need a $arg argument" unless $args{$arg};
    }

    my $self = {};
    bless $self, $class;

    # options should be used.
    $self->host( $args{'host'}         || 127.0.0.1 );
    $self->port( $args{'port'}         || 3306 );
    $self->user( $args{'user'}         || 'audit' );
    $self->password( $args{'password'} || '' );
    $self->charset( $args{'charset'}   || 'utf8' );
    $self->driver( $args{'driver'}     || 'mysql' );

    return $self;
}

sub get_dbh {
    my ( $self, $database, $opts ) = @_;
    $opts ||= {};
    my $host     = $self->{host};
    my $port     = $self->{port};
    my $user     = $self->{user};
    my $password = $self->{password};
    my $charset  = $self->{charset};
    my $driver   = $self->{driver};

    my $defaults = {
        AutoCommit         => 0,
        RaiseError         => 1,
        PrintError         => 0,
        ShowErrorStatement => 1,
        mysql_enable_utf8  => ( $charset =~ m/utf8/i ? 1 : 0 ),
    };
    @{$defaults}{ keys %$opts } = values %$opts;

    if ( $opts->{mysql_use_result} ) {
        $defaults->{mysql_use_result} = 1;
    }

    my $dbh;
    my $tries = 2;
    while ( !$dbh && $tries-- ) {
        PTDEBUG
          && print Dumper(
            join( ', ', map { "$_=>$defaults->{$_}" } keys %$defaults ) );
        $dbh = eval {
            DBI->connect(
                "DBI:$driver:database=$database;host=$host;port=$port",
                $user, $password, $defaults );
        };

        if ( !$dbh && $@ ) {
            if ( $@ =~ m/locate DBD\/mysql/i ) {
                warn
"Cannot connect to MySQL because the Perl DBD::mysql module is "
                  . "not installed or not found.  Run 'perl -MDBD::mysql' to see "
                  . "the directories that Perl searches for DBD::mysql.  If "
                  . "DBD::mysql is not installed, try:\n"
                  . "  Debian/Ubuntu  apt-get install libdbd-mysql-perl\n"
                  . "  RHEL/CentOS    yum install perl-DBD-MySQL\n"
                  . "  OpenSolaris    pgk install pkg:/SUNWapu13dbd-mysql\n";
            }
            elsif ( $@ =~ m/not a compiled character set|character set utf8/i )
            {
                PTDEBUG && print 'Going to try again without utf8 support\n';
                delete $defaults->{mysql_enable_utf8};
            }
            if ( !$tries ) {
                warn "$@";
                return;
            }
       }
    }

    if ( $driver =~ m/mysql/i ) {
        my $sql;
        $sql = 'SELECT @@SQL_MODE';
        PTDEBUG && print "+-- $sql\n";

        my ($sql_mode) = eval { $dbh->selectrow_array($sql) };
        warn "Error getting the current SQL_MORE: $@" if $@;

        if ($charset) {
            $sql = qq{/*!40101 SET NAMES "$charset"*/};
            PTDEBUG && print "+-- $sql\n";
            eval { $dbh->do($sql) };
            warn "Error setting NAMES to $charset: $@" if $@;
            PTDEBUG && print "Enabling charset to STDOUT\n";
            if ( $charset eq 'utf8' ) {
                binmode( STDOUT, ':utf8' )
                  or warn "Can't binmode(STDOUT, ':utf8'): $!\n";
            }
            else {
                binmode(STDOUT) or warn "Can't binmode(STDOUT): $!\n";
            }
        }

        $sql =
            'SET @@SQL_QUOTE_SHOW_CREATE = 1'
          . '/*!40101, @@SQL_MODE=\'NO_AUTO_VALUE_ON_ZERO'
          . ( $sql_mode ? ",$sql_mode" : '' ) . '\'*/';
        PTDEBUG && print "+-- $sql\n";
        eval { $dbh->do($sql) };
        warn "Error setting SQL_QUOTE_SHOW_CREATE, SQL_MODE"
          . ( $sql_mode ? " and $sql_mode" : '' ) . ": $@"
          if $@;
    }

    if (PTDEBUG) {
        print Dumper(
            $dbh->selectrow_hashref(
'SELECT DATABASE(), CONNECTION_ID(), VERSION()/*!50038, @@hostname*/'
            )
        );
        print "+-- 'Connection info:', $dbh->{mysql_hostinfo}\n";
        print Dumper(
            $dbh->selectall_arrayref(
                "SHOW VARIABLES LIKE 'character_set%'",
                { Slice => {} }
            )
        );
        print '+-- $DBD::mysql::VERSION:' . "$DBD::mysql::VERSION\n";
        print '+-- $DBI::VERSION:' . "$DBI::VERSION\n";
    }
    return $dbh;
}

# handle should be destroy.
sub disconnect {
    my ( $self, $dbh ) = @_;
    PTDEBUG && $self->print_active_handles( $self->get_dbh );
    $dbh->disconnect;
}

sub print_active_handles {
    my ( $self, $thing, $level ) = @_;
    $level ||= 0;
    printf(
        "# Active %sh: %s %s %s\n",
        ( $thing->{Type} || 'undef' ),
        "\t" x $level,
        $thing,
        ( ( $thing->{Type} || '' ) eq 'st' ? $thing->{Statement} || '' : '' )
    ) or warn "Cannot print: $OS_ERROR";
    foreach my $handle ( grep { defined } @{ $thing->{ChildHandles} } ) {
        $self->print_active_handles( $handle, $level + 1 );
    }
}

1;
