#!/usr/bin/env perl

=head1 NAME

sys-checkport - check a host port is wether opened or not.

=cut

=head1 SYNOPSIS

    use sys-checkport --help for more info.

    options:
       tcp|t  :  tcp proto, default is true.
       udp|u  :  udp proto, default is false.
       host|h :  host address, default is 127.0.0.1.
       port|p :  port number, must be specified.
       help|h :  print help info.
       verbose|v :  print source source ip address and port.

=cut

use strict;
use warnings;
use IO::Socket::INET;
use Data::Dumper;
use Getopt::Long;

my $tcp  = 1;
my $udp  = 0;
my $host = '127.0.0.1';
my $port;
my $verbose;
my $help;

GetOptions(
    "tcp|t!"     => \$tcp,
    "udp|u!"     => \$udp,
    "host|h=s"   => \$host,
    "port|p=i"   => \$port,
    "help!"      => \$help,
    "verbose|v!" => \$verbose,
);

if ($help) {
    usage($0);
}

sub usage {
    my $name = shift;
    system("perldoc $name");
    exit 0;
}

if ($udp) {
    $tcp = 0;
}

unless ( ( $tcp & $udp ) == 0 ) {
    print "both tcp and udp is not allowed\n";
    exit 1;
}

unless ($port) {
    print "no port number. use --help for more info\n";
    exit 1;
}

# tcp/udp port connected
$| = 1;
my $proto  = $udp == 0 ? "tcp" : "udp";
my $fail   = 0;
my $socket;

if ($udp) {
  $socket = IO::Socket::INET->new(
      PeerAddr => $host,
      PeerPort => $port,
      Proto    => $proto,
      Timeout  => 2
  );

  eval {
      $socket->send('port check');
  };
  if ($@) {
    $fail++;
    goto LABEL_CALC;
  }

  # wait to receive ICMP unreachable
  # read more from
  #   https://serverfault.com/questions/416205/testing-udp-port-connectivity
  #   http://en.wikipedia.org/wiki/ICMP_Destination_Unreachable
  sleep(1);

  $socket->blocking(0);
  if (! $socket->recv(my $buf, 0)) {
    # will get ECONNREFUSED on UNIX, other error cannot determine port state
    if (! $!{EAGAIN}) {
      $fail++;
    }
  }
}
else {
  $socket = IO::Socket::INET->new(
      PeerAddr => $host,
      PeerPort => $port,
      Proto    => $proto,
      Timeout  => 2
  ) || $fail++;
}

LABEL_CALC:

my $str = do {
    if ( $fail > 0 ) {
        "port $port: closed";
    }
    else {
        my $list = "port $port: open";
        if ($udp) {
            $list .= " | filtered -- udp port has no response";
        }
        if ($verbose) {
            $list .=
                "\n  source info "
              . $socket->sockhost() . ":"
              . $socket->sockport();
        }
        $list;
    }
};

print $str . "\n";

=head1 AUTHOR

zhe.chen <chenzhe07@gmail.com>

=head1 CHANGELOG

v0.1.0 version

=cut
